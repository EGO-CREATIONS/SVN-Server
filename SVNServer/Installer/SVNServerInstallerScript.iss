; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define Major
#define Minor
#define Rev
#define Build

#define AppName "SVN Server"
;#define AppVersion "1.0"
#define AppVersion GetVersionComponents("..\Files\bin\svn.exe", Major, Minor, Rev, Build), Str(Major) + "." + Str(Minor) + "." + Str(Rev)
#define AppPublisher "EGO-CREATIONS"
#define AppURL "https://www.ego-creations.de"
#define AppCopyright "Copyright © 2024, " + AppPublisher
#define SetupIcon ".\Res\SVN.ico"

#define SVNVersion GetVersionComponents("..\Files\bin\svn.exe", Major, Minor, Rev, Build)
#define SVNRootDir "\repos"
#define SVNSvcName "SVNServer"
#define SVNSvcDisplayName AppName
#define SVNSvcDesc "Startet und verwaltet einen lokalen " + AppName + "."
#define SVNFWPort "3690"
#define SVNFWName "SVN Protocol Port 3690"
#define SVNTaskSchedulerName "SVN Server Agent"

#define AdminGroupName "Administrators"
#define AdminName "admin"
#define AdminPwd "admin"

#define RegEnvVars "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"
#define AppRegPath "SOFTWARE\WOW6432Node\EGO-CREATIONS"

#define OutputFilename SVNSvcName + "_Setup_" + AppVersion

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{3EC930B7-848B-4A75-9B1C-46C18B980915}
AppName={#AppName}
AppVersion={#AppVersion}
;AppVerName={#AppName} {#AppVersion}
AppPublisher={#AppPublisher}
AppPublisherURL={#AppURL}
AppSupportURL={#AppURL}
AppUpdatesURL={#AppURL}
DefaultDirName={commonpf64}\{#AppName}
DefaultGroupName={#AppName}
DisableProgramGroupPage=yes
OutputDir=.\Output
OutputBaseFilename={#OutputFilename}
Compression=lzma
SolidCompression=yes
ChangesEnvironment=yes
PrivilegesRequired=admin
UninstallDisplayName=SVN Server
UninstallDisplayIcon={app}\bin\svn.exe
AllowRootDirectory=True
SetupIconFile={#SetupIcon}
;SignTool=Signtool
;SignedUninstaller=yes
AppCopyright={#AppCopyright}
VersionInfoVersion={#SVNVersion}
VersionInfoCompany={#AppPublisher}
VersionInfoDescription=SVN Server Installation
VersionInfoCopyright={#AppCopyright}
VersionInfoProductName={#AppName}
VersionInfoProductVersion={#AppVersion}
VersionInfoOriginalFileName={#OutputFilename}.exe
ArchitecturesInstallIn64BitMode=x64
AlwaysRestart=yes
MinVersion=0,10.0

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Icons]
Name: "{group}\{cm:ProgramOnTheWeb,{#AppName}}"; Filename: "{#AppURL}"
Name: "{group}\{cm:UninstallProgram,{#AppName}}"; Filename: "{uninstallexe}"

[INI]
Filename: "{app}\{code:GetSvcInfo|0}.url"; Section: "InternetShortcut"; Key: "URL"; String: "{#AppURL}"
Filename: "{app}\conf\svnserve.conf"; Section: "general"; Key: "password-db"; String: "{app}\conf\passwd.conf"
Filename: "{app}\conf\svnserve.conf"; Section: "general"; Key: "authz-db"; String: "{app}\conf\authz.conf"
// TODO
Filename: "{app}\conf\authz.conf"; Section: "groups"; Key: "{#AdminGroupName}"; String: "{code:GetSvnAdminName}"
Filename: "{app}\conf\authz.conf"; Section: "/"; Key: "{code:GetSvnAdminName}"; String: "rw"
Filename: "{app}\conf\passwd.conf"; Section: "users"; Key: "{code:GetSvnAdminName}"; String: "{code:GetSvnAdminPwd}"

[Registry]
Root: "HKLM"; Subkey: "{#RegEnvVars}"; ValueType: string; ValueName: "SVN_EDITOR"; ValueData: "notepad"; Flags: uninsdeletevalue
Root: "HKLM"; Subkey: "{#RegEnvVars}"; ValueType: string; ValueName: "SVN_HOME"; ValueData: "{app}"; Flags: uninsdeletevalue
Root: "HKLM"; Subkey: "{#RegEnvVars}"; ValueType: string; ValueName: "SVN_BINDIR"; ValueData: "{app}\bin"; Flags: uninsdeletevalue
Root: "HKLM"; Subkey: "{#RegEnvVars}"; ValueType: string; ValueName: "SVNPath"; ValueData: "{code:GetReposDir}"; Flags: uninsdeletevalue
Root: "HKLM"; Subkey: "{#RegEnvVars}"; ValueType: string; ValueName: "Path"; ValueData: "{olddata};%SVN_HOME%;%SVN_BINDIR%;%SVNPath%"

Root: "HKLM"; Subkey: "{#AppRegPath}\{#AppName}"; ValueType: string; ValueName: "ServiceName"; ValueData: "{code:GetSvcInfo|0}"; Flags: uninsdeletevalue uninsdeletekeyifempty
Root: "HKLM"; Subkey: "{#AppRegPath}\{#AppName}"; ValueType: string; ValueName: "ServiceDesc"; ValueData: "{code:GetSvcInfo|2}"; Flags: uninsdeletevalue uninsdeletekeyifempty
Root: "HKLM"; Subkey: "{#AppRegPath}\{#AppName}"; ValueType: string; ValueName: "ServiceDisplayName"; ValueData: "{code:GetSvcInfo|1}"; Flags: uninsdeletevalue uninsdeletekeyifempty
Root: "HKLM"; Subkey: "{#AppRegPath}\{#AppName}"; ValueType: string; ValueName: "SVNPath"; ValueData: "{code:GetReposDir}"; Flags: uninsdeletevalue uninsdeletekeyifempty
Root: "HKLM"; Subkey: "{#AppRegPath}\{#AppName}"; ValueType: string; ValueName: "SVN_HOME"; ValueData: "{app}"; Flags: uninsdeletevalue uninsdeletekeyifempty
Root: "HKLM"; Subkey: "{#AppRegPath}\{#AppName}"; ValueType: string; ValueName: "SVN_BINDIR"; ValueData: "{app}\bin"; Flags: uninsdeletevalue uninsdeletekeyifempty
Root: "HKLM"; Subkey: "{#AppRegPath}\{#AppName}"; ValueType: string; ValueName: "SVN_EDITOR"; ValueData: "C:\Windows\notepad.exe"; Flags: uninsdeletevalue uninsdeletekeyifempty
Root: "HKLM"; Subkey: "{#AppRegPath}\{#AppName}"; ValueType: string; ValueName: "Version"; ValueData: "{#AppVersion}"; Flags: uninsdeletevalue uninsdeletekeyifempty

[Dirs]
Name: "{app}\agent"; Flags: uninsalwaysuninstall
Name: "{app}\bin"; Flags: uninsalwaysuninstall
Name: "{app}\conf"; Flags: uninsalwaysuninstall
Name: "{app}\Licenses"; Flags: uninsalwaysuninstall
Name: "{code:GetReposDir}"
Name: "{app}"; Flags: uninsalwaysuninstall

[Files]
// temporary files
Source: ".\Res\Databases.ico"; Flags: dontcopy noencryption
Source: ".\Res\ServiceController.ico"; Flags: dontcopy noencryption
Source: ".\Res\ReposAdminUser.ico"; Flags: dontcopy noencryption
Source: ".\Res\ScheduledTaskConfig.xml"; Flags: dontcopy noencryption
// 
Source: "..\Files\agent\SVNServerAgent.exe"; DestDir: "{app}\agent"
Source: "..\Files\bin\libapr-1.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "..\Files\bin\libaprutil-1.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "..\Files\bin\libsvn_client-1.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "..\Files\bin\libsvn_delta-1.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "..\Files\bin\libsvn_diff-1.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "..\Files\bin\libsvn_fs-1.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "..\Files\bin\libsvn_fs_fs-1.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "..\Files\bin\libsvn_fs_util-1.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "..\Files\bin\libsvn_fs_x-1.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "..\Files\bin\libsvn_ra-1.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "..\Files\bin\libsvn_repos-1.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "..\Files\bin\libsvn_subr-1.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "..\Files\bin\libsvn_wc-1.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "..\Files\bin\msvcp100.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "..\Files\bin\msvcr100.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "..\Files\bin\svn.exe"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "..\Files\bin\svnadmin.exe"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "..\Files\bin\svnbench.exe"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "..\Files\bin\svndumpfilter.exe"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "..\Files\bin\svnfsfs.exe"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "..\Files\bin\svnlook.exe"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "..\Files\bin\svnmucc.exe"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "..\Files\bin\svnrdump.exe"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "..\Files\bin\svnserve.exe"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "..\Files\bin\svnsync.exe"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "..\Files\bin\svnversion.exe"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "..\Files\Licenses\APR License.txt"; DestDir: "{app}\Licenses"; Flags: ignoreversion
Source: "..\Files\Licenses\APR Util License.txt"; DestDir: "{app}\Licenses"; Flags: ignoreversion
Source: "..\Files\Licenses\OpenSSL License.txt"; DestDir: "{app}\Licenses"; Flags: ignoreversion
Source: "..\Files\Licenses\Subversion License.txt"; DestDir: "{app}\Licenses"; Flags: ignoreversion
Source: "..\Files\Licenses\ZLib License.txt"; DestDir: "{app}\Licenses"; Flags: ignoreversion
Source: "..\Files\conf\authz.conf"; DestDir: "{app}\conf"; Flags: ignoreversion
Source: "..\Files\conf\passwd.conf"; DestDir: "{app}\conf"; Flags: ignoreversion
Source: "..\Files\conf\svnserve.conf"; DestDir: "{app}\conf"; Flags: ignoreversion
Source: "..\Files\conf\svnserve.conf.template"; DestDir: "{app}\conf"; Flags: ignoreversion

[Run]
Filename: "{sys}\sc.exe"; Parameters: "create {code:GetSvcInfo|0} binPath=""\""{app}\bin\svnserve.exe\"" --service --root \""{code:GetReposDir}\"" "" displayname=""{code:GetSvcInfo|1}"" depend=Tcpip start={code:GetSvcInfo|3}"; Flags: runhidden shellexec
Filename: "{sys}\sc.exe"; Parameters: "description {code:GetSvcInfo|0} ""{code:GetSvcInfo|2}"""; Flags: runhidden shellexec
Filename: "{sys}\sc.exe"; Parameters: "start {code:GetSvcInfo|0}"; Flags: runhidden shellexec
Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall add rule name=""{#SVNFWName}"" dir=in action=allow protocol=TCP localport={#SVNFWPort}"; Flags: runhidden shellexec
Filename: "{app}\agent\SVNServerAgent.exe"; Flags: runhidden shellexec runasoriginaluser; Check: CreateScheduledTask()

[UninstallRun]
Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall delete rule name=""{#SVNFWName}"""; Flags: runhidden shellexec waituntilterminated
Filename: "{sys}\schtasks.exe"; Parameters: "/delete /F /TN ""{#SVNTaskSchedulerName}"""; Flags: runhidden shellexec waituntilterminated
Filename: "{sys}\taskkill.exe"; Parameters: "/IM ""svnserve.exe"" /F /T"; Flags: runhidden shellexec waituntilterminated
Filename: "{sys}\sc.exe"; Parameters: "delete {code:GetSvcInfo|0}"; Flags: runhidden shellexec waituntilterminated
Filename: "{sys}\sc.exe"; Parameters: "stop {code:GetSvcInfo|0}"; Flags: runhidden shellexec waituntilterminated
Filename: "{sys}\taskkill.exe"; Parameters: "/IM ""SVNServerAgent.exe"" /F /T"; Flags: runhidden shellexec waituntilterminated

[UninstallDelete]
Type: filesandordirs; Name: "{app}"
Type: dirifempty; Name: "{app}"

[Messages]
BeveledLabel={#AppCopyright}
SetupWindowTitle=Setup - {#AppName} {#AppVersion}

; Code and Dialogs included here
#include ".\Src\SVNServerInstaller_BaseCode.iss"
